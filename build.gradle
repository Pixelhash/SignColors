buildscript {
    ext.kotlin_version = '1.2.51'
    ext.shadow_version = '2.0.2'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "gradle.plugin.io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.0.RC6-4"
    }
}

group 'de.codehat'
version '1.2.0'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: 'java'
apply plugin: 'kotlin'

final def BUKKIT_VERSION = '1.13-R0.1-SNAPSHOT'
final def VAULT_VERSION = '1.6'
final def BSTATS_VERSION = '1.2'
final def UPDATER_VERSION = '1.0.0'
final def ORMLITE_VERSION = '5.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }
    maven {
        name 'Sonatype'
        url 'https://oss.sonatype.org/content/groups/public'
    }
    maven {
        name 'vault-repo'
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }
    maven {
        name 'bstats'
        url "http://repo.bstats.org/content/repositories/releases"
    }
    maven {
        name 'CodeHat Bintray'
        url  "https://dl.bintray.com/codehat/maven-repo"
    }
    mavenLocal()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly "org.spigotmc:spigot-api:$BUKKIT_VERSION"
    compileOnly "net.milkbowl.vault:VaultAPI:$VAULT_VERSION"
    compile "com.j256.ormlite:ormlite-core:$ORMLITE_VERSION"
    compile "com.j256.ormlite:ormlite-jdbc:$ORMLITE_VERSION"
    compile "de.codehat:plugin-update-checker:$UPDATER_VERSION"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    classifier = null

    relocate 'de.codehat.pluginupdatechecker', 'de.codehat.signcolors.update'
}

detekt {
    version = "1.0.0.RC6-4"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task deploy(dependsOn: shadowJar, type: Copy) {
    from "build/libs"
    into "D:\\Minecraft\\PluginTest-Server\\plugins"
    include "*.jar"
}